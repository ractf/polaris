allprojects {
    apply plugin: 'java'
    apply plugin: uk.co.ractf.polaris.PolarisPlugin
    apply plugin: 'jacoco'

    group = 'uk.co.ractf'
    version = '0.0.75'

    repositories {
        maven { url "https://repo1.maven.org/maven2/" }
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        options.deprecation = true
    }
}

subprojects {
    ext {
        dropwizardVersion = '2.0.21'
    }

    test.useTestNG()
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled true
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}


tasks.create(name: 'chmodGitHooks') {
    Runtime.getRuntime().exec("chmod -R +x .git/hooks/");
}

task installGitHooks(type: Copy) {
    from new File(rootProject.rootDir, 'scripts/pre-commit.py')
    into { new File(rootProject.rootDir, '.git/hooks/') }
    rename("pre-commit\\.py", "pre-commit")
}

chmodGitHooks.dependsOn installGitHooks
clean.dependsOn chmodGitHooks
