import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
        classpath "com.netflix.nebula:gradle-ospackage-plugin:8.5.1"
        classpath "com.palantir.gradle.gitversion:gradle-git-version:0.12.3"
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'nebula.ospackage'
apply plugin: 'com.palantir.git-version'

mainClassName = 'uk.co.ractf.polaris.PolarisApplication'
group = 'uk.co.ractf'
version = '0.0.2'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':polaris-api')
    implementation 'io.dropwizard:dropwizard-core:2.0.19'
    implementation 'io.dropwizard:dropwizard-auth:2.0.19'
    implementation 'com.smoketurner.dropwizard:consul-core:2.0.12-1'
    implementation 'com.smoketurner.dropwizard:consul-ribbon:2.0.12-1'
    implementation "io.swagger.core.v3:swagger-core:2.1.7"
    implementation "io.swagger.core.v3:swagger-jaxrs2:2.1.7"
    implementation 'io.swagger.core.v3:swagger-integration:2.1.7'
    implementation 'com.github.docker-java:docker-java-core:3.2.7'
    implementation 'com.github.docker-java:docker-java-transport-httpclient5:3.2.7'
    implementation 'org.jetbrains:annotations:20.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}
shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC"))
                .format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
    archiveName 'polaris.jar'
}

buildRpm {
    packageName = 'polaris'
    def details = versionDetails(prefix: 'rc@')
    version = details.lastTag
    release = rootProject.version
    arch = 'X86_64'
    os = 'LINUX'
    license = 'AGPL'
    url = 'https://github.com/ractf/polaris'
    vendor = 'RACTF'
    packageDescription = 'The RACTF challenge scheduler'
    maintainer = 'RACTF <admins@ractf.co.uk>'

    requires('systemd')
    requires('java-11-openjdk')
    requires('docker-ce')

    from('src/main/dist/bin') {
        into '/etc/systemd/system'
        addParentDirs = false
    }

    from('src/main/dist/resources') {
        fileType CONFIG | NOREPLACE
        into '/etc/polaris'
        createDirectoryEntry = true
    }

    from(shadowJar.outputs.files) {
        into '/opt/polaris'
        createDirectoryEntry = true
    }
}
